plugins {
    id 'java-library'
    id 'maven-publish'
	id 'io.spring.dependency-management' version '1.1.7'
    id("pl.allegro.tech.build.axion-release") version "1.20.1"
}

group = "io.github.h3yzack"
version = scmVersion.version


repositories {
    mavenCentral()
}

dependencies {

    api 'commons-codec:commons-codec:1.15'

    compileOnly "org.springframework:spring-webflux"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.5'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named('test') {
    useJUnitPlatform()
}

// Add publishing configuration
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            
            groupId = project.group
            artifactId = 'aurasage-core'
            version = project.version
            
            pom {
                name = 'Aurasage Core'
                description = 'Core library for Aurasage application'
                
                developers {
                    developer {
                        id = 'h3yzack'
                        name = 'Zuhaimi A.'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/h3yzack/aurasage-core")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") as String
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key") as String
            }
        }
    }
}